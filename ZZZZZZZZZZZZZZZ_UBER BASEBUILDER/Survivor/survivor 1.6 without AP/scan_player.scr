main:

	local.team = self.dmteam

	while(self != NIL && self != NULL && self.dmteam == local.team && self.lives > 0)
	{
		wait 1
	}
	
//level.players--
	if(level.team != 1)
	{
		level.players--
	}
	else
	{
		level.players[local.team]--
	}

	thread double_check

	if(self != NULL && self != NIL)
	{
		if(self.dmteam == "spectator" || self.lives <= 0)
		{	
		
			if(self != NIL && self != NULL)
			{
				self iprint "You have been eliminated from the game"
				self.playing = 0
			}

			exec Survivor/hud.scr ("A player has been elimnated")
			wait 2	
		}
		else
		{
			if(level.team != 1)
			{
				self spectator //its FFA and they have 
			}
			else
			{
				//they ahve swapped teams
				level.players[self.dmteam]++
				exec survivor/scan_player.scr
			}
		}
	}

	local.end = 0

	if(level.team == 1)
	{
		if(level.players["axis"] <= 0 || level.players["allies"]  <= 0)
		{
			local.end = 1
		}
		
	}
	else
	{
		if(level.players <= 1)
		{
			local.end = 1
		}

	}

	if(local.end == 1)
	{
		level.roundend = 1
		exec Survivor/hud.scr ("The Battle is over")
		exec survivor/roundend.scr 
	}
/*
	else
	{
		if(self != NIL && self != NULL)
		{
			if(self.lives <=0)
			{
				self waitexec survivor/camera.scr
				self exec survivor/spectator.scr
			}
		}
	}

*/
	if(level.team == 1)
	{
		exec Survivor/hud.scr (  level.players["axis"] + " Axis and " + level.players["allies"] + " Allied " + " left")
	}
	else
	{
		exec Survivor/hud.scr (level.players + " Players left")
	}


end


double_check:

	local.players["axis"] = 0
	local.players["allies"] = 0

	for(local.i = 1; local.i <= $player.size;local.i++)
	{
		local.player = $player[local.i]

		if(local.player.dmteam != "spectator")
		{
			if(local.player.playing == 1)
			{
				local.players[local.player.dmteam]++
			}
		}
	}

	if(level.team != 1)
	{
		local.people = local.players["axis"] + local.players["allies"]
	
		if(level.players != local.people)
		{
			println "There seems to be a problem with the player count in FFA"
			level.players = local.people
		}
	}
	else
	{
		if(level.players["axis"] != local.players["axis"] || level.players["allies"] != local.players["allies"])
		{
			println "There seems to be a problem with the player count"
			level.players["axis"] = local.players["axis"] 
			level.players["allies"] = local.players["allies"]
		}
	}
end